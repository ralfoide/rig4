import org.ajoberstar.grgit.Grgit

plugins {
    id "java"
    id "org.ajoberstar.grgit" version "4.1.1"
}

group "com.alflabs.rig4"
version "0.1-Exp"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext.dagger = [
        generatedSrcDir: "src/dagger/java"
]


configurations {
    // Important: the fatJar rule below only packages stuff from the "compileClasspath"
    // configuration. It does not package stuff from "implementation" or "api" ones.
    // Note: "compileClasspath" works for fatJar, however it breaks with unit tests
    // so going back to the old "compile" instead. Since it's deprecated, we recreate
    // something that works for both compile + runtime + fatJar.
    runtimeCompile {
        canBeResolved = true
        canBeConsumed = true
    }
    compileClasspath.extendsFrom(runtimeCompile)
    runtimeClasspath.extendsFrom(runtimeCompile)
    testCompileClasspath.extendsFrom(runtimeCompile)
    testRuntimeClasspath.extendsFrom(runtimeCompile)
}

dependencies {
    runtimeCompile project(":LibUtilsJava")
    runtimeCompile "com.fasterxml.jackson.core:jackson-databind:2.12.7.1"
    runtimeCompile "org.jsoup:jsoup:1.15.3"
    runtimeCompile "net.coobird:thumbnailator:0.4.8"

    runtimeCompile "com.google.apis:google-api-services-drive:v3-rev20211107-1.32.1"
    runtimeCompile "com.google.api-client:google-api-client:1.35.2"
    runtimeCompile "com.google.oauth-client:google-oauth-client-jetty:1.34.1"
    runtimeCompile "com.google.http-client:google-http-client-jackson2:1.43.1"
    runtimeCompile "com.google.guava:guava:30.0-jre"   // 20.0 needed by google-api-services-drive

    runtimeCompile "com.google.dagger:dagger:2.11"
    runtimeCompile "javax.annotation:javax.annotation-api:1.3.2"
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"
    annotationProcessor "javax.annotation:javax.annotation-api:1.3.2"

    runtimeCompile "com.google.auto.factory:auto-factory:1.0-beta5"
    annotationProcessor "com.google.auto.factory:auto-factory:1.0-beta5"

    testImplementation "junit:junit:4.13.1"
    testImplementation "com.google.truth:truth:0.31"
    testImplementation "org.mockito:mockito-core:2.2.9"
    testImplementation "com.google.guava:guava:30.0-jre"
}

jar {
    manifest {
        attributes(
            "Main-Class": "com.alflabs.rig4.EntryPoint",
        )
    }
}

// Build a "fat" jar with all the dependencies.
// Update: this uses "runtimeCompile" and ignores "implementation".
task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = "all"
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

compileJava {
    project.mkdir(dagger.generatedSrcDir)
    // Options are listed in https://docs.gradle.org/current/dsl/org.gradle.api.tasks.compile.CompileOptions.html
    // Note: becomes generatedSourceOutputDirectory in/after Gradle 6.6.1
    def f = new File(project.projectDir, dagger.generatedSrcDir)
    logger.warn("Dagger generated src dir = " + f.absolutePath)
    options.annotationProcessorGeneratedSourcesDirectory = f
    outputs.dir(dagger.generatedSrcDir)

    // Generate a resource file with the jar version + git head hash
    // (this file should be in gitignore).
    def versionDir  = new File("$buildDir", "../src/main/resources/com/alflabs/rig4").getCanonicalFile()
    if (!versionDir.isDirectory()) {
        logger.warn("Version file: creating directory " + versionDir.getPath())
        versionDir.mkdirs()
    }
    def versionFile = new File(versionDir, "version.txt")
    logger.info("Version file: " + versionFile.getPath())

    def git = Grgit.open(dir: project.rootDir.path)
    def revision = git.head().abbreviatedId
    logger.warn("Git rev " + revision + " at " + project.rootDir.path)
    versionFile.write(project.version + "-" + revision)
}

clean {
    delete dagger.generatedSrcDir
}

task run(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.alflabs.rig4.EntryPoint"
}
