plugins {
    id "java"
    // Add the Annotation Processor Plugin to run dagger at build-time.
    id "net.ltgt.apt" version "0.10"
    id "org.ajoberstar.grgit" version "2.1.0"
}

group "com.alflabs.rig4"
version "1.0-SNAPSHOT"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext.dagger = [
        generatedSrcDir: "src/dagger/java"
]

sourceSets {
    main {
        output.generatedSourcesDir = dagger.generatedSrcDir
    }
}

dependencies {
    compile project(":LibUtilsJava")
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7.1"
    compile "org.jsoup:jsoup:1.11.2"
    compile "net.coobird:thumbnailator:0.4.8"

    compile "com.google.apis:google-api-services-drive:v3-rev90-1.23.0"
    compile "com.google.oauth-client:google-oauth-client-jetty:1.23.0"
    compile "com.google.http-client:google-http-client-jackson2:1.23.0"
    compile "com.google.guava:guava:20.0"   // 20.0 needed by google-api-services-drive

    compile "com.google.dagger:dagger:2.11"
    apt     "com.google.dagger:dagger-compiler:2.11"

    compile "com.google.auto.factory:auto-factory:1.0-beta5"
    apt     "com.google.auto.factory:auto-factory:1.0-beta5"

    testCompile "junit:junit:4.10"
    testCompile "com.google.truth:truth:0.31"
    testCompile "org.mockito:mockito-core:2.2.9"
    testCompile "com.google.guava:guava:20.0"
}

jar {
    manifest {
        attributes(
            "Main-Class": "com.alflabs.rig4.EntryPoint",
        )
    }
}

// Build a "fat" jar with all the dependencies.
task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = "all"
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

compileJava {
    delete dagger.generatedSrcDir
    source dagger.generatedSrcDir

    // Generate a resource file with the jar version + git head hash
    // (this file should be in gitignore).
    def versionFile = new File("$buildDir/../src/main/resources/com/alflabs/rig4/version.txt")
    def git = org.ajoberstar.grgit.Grgit.open(dir: "..")
    def revision = git.head().abbreviatedId
    versionFile.write(project.version + "-" + revision)
}

clean {
    delete dagger.generatedSrcDir
}
